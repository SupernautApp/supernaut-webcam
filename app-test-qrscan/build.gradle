plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

apply plugin: 'org.beryx.jlink'

sourceCompatibility = 11
targetCompatibility = 11

def appName = 'SupernautTestQrScan'

dependencies {
    implementation project(":cameraqr")
    implementation project(":qrimage")

    implementation 'com.google.zxing:javase:3.5.1'

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    runtimeOnly "org.slf4j:slf4j-jdk14:${slf4jVersion}"
}

javafx {
    version = javaFxVersion
    modules = [ 'javafx.controls', 'javafx.swing' ]
}

application {
    mainModule = 'app.supernaut.camera.app.testqrscan'
    mainClassName = 'app.supernaut.camera.app.testqrscan.TestQrScannerApplication'
}

def os = org.gradle.internal.os.OperatingSystem.current()

jlink {
    addExtraDependencies("javafx")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--add-modules', 'jdk.charsets,java.logging,org.slf4j.jul']
    launcher {
        name = appName
        jvmArgs = []
    }
    mergedModule {
        requires 'java.logging'
        requires 'java.desktop'
        requires 'jdk.charsets'
        //requires 'org.slf4j'
    }
    jpackage {
        // See https://badass-jlink-plugin.beryx.org/releases/latest/#_jpackage for
        // where the plugin's jpackage task finds the path to the jpackage tool by default
        skipInstaller = true

        // Massage version string to be compatible with jpackage installers
        // for the current OS platform
        def appVersionForJpackage = 1.0

        imageOptions = ["--verbose", "--app-version", appVersionForJpackage]
        installerOptions = ["--app-version", appVersionForJpackage]
        if (os.macOsX) {
            //imageOptions += [ '--resource-dir', "${projectDir}/src/macos/resource-dir" ]
        } else if (os.windows) {
            installerOptions += ['--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
    }
}

